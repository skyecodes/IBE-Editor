allprojects {
    apply plugin: "java"
    java.toolchain.languageVersion = JavaLanguageVersion.of(16)
    repositories {
        mavenCentral()
        maven {
            name = "Minecraft Libraries"
            url = "https://libraries.minecraft.net"
        }
    }
}

group = "com.github.franckyi"
version = project.mod_version

task buildFabric(type: Jar) {
    dependsOn buildDep("gameadapter-fabric"),
            buildDep("guapi-fabric"),
            buildDep("guapivanillatheme-fabric"),
            buildDep("ibeeditor-fabric")
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveBaseName.set("IBEEditor-Fabric-$minecraft_version")
    from buildJar("databindings-api"),
            buildJar("databindings-base"),
            buildJar("gameadapter-api"),
            buildJar("gameadapter-base"),
            buildJar("guapi-api"),
            buildJar("guapi-base"),
            buildJar("guapivanillatheme-base"),
            buildJar("ibeeditor-base"),
            buildJar("ibeeditor-fabric")
    exclude("assets/guapivanillatheme/")
    manifest {
        attributes([
                "Specification-Title"     : "ibeeditor",
                "Specification-Vendor"    : "Franckyi",
                "Specification-Version"   : "1",
                "Implementation-Title"    : "ibeeditor",
                "Implementation-Version"  : "${mod_version}",
                "Implementation-Vendor"   : "Franckyi",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

task buildForge(type: Jar) {
    dependsOn buildDep("gameadapter-forge"),
            buildDep("guapi-forge"),
            buildDep("guapivanillatheme-forge"),
            buildDep("ibeeditor-forge")
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveBaseName.set("IBEEditor-Forge-$minecraft_version")
    from buildJar("databindings-api"),
            buildJar("databindings-base"),
            buildJar("gameadapter-api"),
            buildJar("gameadapter-base"),
            buildJar("gameadapter-forge"),
            buildJar("guapi-api"),
            buildJar("guapi-base"),
            buildJar("guapi-forge"),
            buildJar("guapivanillatheme-base"),
            buildJar("guapivanillatheme-forge"),
            buildJar("ibeeditor-base"),
            buildJar("ibeeditor-forge")
    exclude("META-INF/mods.toml")
    rename("build.mods.toml", "mods.toml")
    manifest {
        attributes([
                "Specification-Title"     : "ibeeditor",
                "Specification-Vendor"    : "Franckyi",
                "Specification-Version"   : "1",
                "Implementation-Title"    : "ibeeditor",
                "Implementation-Version"  : "${mod_version}",
                "Implementation-Vendor"   : "Franckyi",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs"            : "guapivanillatheme-forge.mixins.json,gameadapter-forge.mixins.json"
        ])
    }
    finalizedBy ":ibeeditor-forge:signForgeJar"
}

task buildAll() {
    dependsOn allprojects.clean
    finalizedBy buildForge, buildFabric
}

Task buildDep(String dependency) {
    return project(":" + dependency).build
}

FileTree buildJar(String dependency) {
    return project(":" + dependency).zipTree("build/libs/" + dependency + ".jar")
}
