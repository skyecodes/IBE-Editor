plugins {
    id "fabric-loom" version "$loom_version"
}

repositories {
    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/releases/"
    }
}

archivesBaseName = 'ibeeditor-fabric'

dependencies {
    minecraft "com.mojang:minecraft:$minecraft_version"
    mappings "net.fabricmc:yarn:$yarn_mappings:v2"
    modImplementation "net.fabricmc:fabric-loader:$loader_version"
    modImplementation "net.fabricmc.fabric-api:fabric-api:$fabric_version"
    modImplementation("com.terraformersmc:modmenu:2.0.5") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    implementation project(':databindings-api')
    implementation project(':databindings-base')
    implementation project(':gameadapter-api')
    implementation project(':gameadapter-fabric')
    implementation project(':guapi-api')
    implementation project(':guapi-base')
    implementation project(':guapi-fabric')
    implementation project(':guapivanillatheme-base')
    implementation project(':guapivanillatheme-fabric')
    implementation project(':ibeeditor-base')
    include project(':gameadapter-fabric')
    include project(':guapi-fabric')
    include project(':guapivanillatheme-fabric')
}

processResources {
    from "src/main/resources", "../ibeeditor-base/src/main/resources"

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    inputs.property "version", project.mod_version

    filesMatching("fabric.mod.json") {
        expand "version": project.mod_version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 16
}

loom {
    runs {
        client {
            vmArg "-Dfabric.log.level=debug"
        }
        server {
            vmArg "-Dfabric.log.level=debug"
        }
    }
}
